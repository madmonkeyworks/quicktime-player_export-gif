local currentFilePath
local startTime
local gifDuration
local defaultGifDuration
local title
local exportDir
local exportPath
local exportFileNamePrefix
local fileExt

# set default values
set defaultGifDuration to 3
set exportFileNamePrefix to ""
set fileExt to "gif"

# get values from quicktime player
tell application "QuickTime Player"
	
	# get current file path
	set currentFilePath to the file of the front document
	
	set startTime to get the (current time of the front document) div 1
	
	# get duration from user dialog
	set gifDuration to text returned of (display dialog "Duration? (in seconds)" default answer defaultGifDuration with icon 1)
	
	# get title from user dialog
	set title to text returned of (display dialog "Title?" default answer "title" with icon 1)
	
end tell

# set export paths and convert to POSIX
set exportDir to choose folder with prompt "Select an output folder:"
set exportPath to POSIX path of exportDir & exportFileNamePrefix & title & "." & fileExt
set currentFilePath to POSIX path of currentFilePath

# use ffmpeg to export the gif
tell application "Terminal"
	do script "ffmpeg -ss " & startTime & " -t " & gifDuration & " -i " & quoted form of currentFilePath & " -vf \"fps=5,scale=580:-1:flags=lanczos\" -loop 0 " & quoted form of exportPath
end tell
